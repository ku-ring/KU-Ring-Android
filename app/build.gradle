plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.android.gms.oss-licenses-plugin'
}

def keystorePropertiesFile = rootProject.file("app/signing/keystore.properties")
def localPropertiesFile = rootProject.file("local.properties")

android {
    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                storeFile file(keystoreProperties['storeFile'])
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    compileSdk 30

    defaultConfig {
        applicationId "com.ku_stacks.ku_ring"
        minSdk 23
        targetSdk 30
        versionCode 11
        versionName "1.1.3"

        def localProperties = new Properties()
        localProperties.load(new FileInputStream(localPropertiesFile))

        buildConfigField "String", "APPS_FLYER_DEV_KEY", localProperties['APPS_FLYER_DEV_KEY']

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    viewBinding {
        enabled = true
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        debug {
            minifyEnabled false
            manifestPlaceholders = [
                    appName : "@string/app_name_debug",
                    appIcon : "@drawable/ic_ku_ring_launcher_dev"
            ]
            buildConfigField "String", "API_BASE_URL", "\"https://kuring-dev.herokuapp.com/api/v1/\""
            buildConfigField "String", "WEB_SOCKET_URL", "\"wss://kuring-dev.herokuapp.com/kuring/search\""

            applicationIdSuffix '.debug'
        }

        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            manifestPlaceholders = [
                    appName : "@string/app_name",
                    appIcon : "@drawable/ic_ku_ring_launcher"
            ]
            buildConfigField "String", "API_BASE_URL", "\"https://kuring.herokuapp.com/api/v1/\""
            buildConfigField "String", "WEB_SOCKET_URL", "\"wss://kuring.herokuapp.com/kuring/search\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'com.google.firebase:firebase-messaging-ktx:21.0.1'

    // dagger hilt
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    implementation 'com.google.dagger:hilt-android:2.39'
    kapt 'com.google.dagger:hilt-compiler:2.39'
    androidTestImplementation  'com.google.dagger:hilt-android-testing:2.39'
    kaptAndroidTest 'com.google.dagger:hilt-compiler:2.39'
    testImplementation 'com.google.dagger:hilt-android-testing:2.39'
    kaptTest 'com.google.dagger:hilt-compiler:2.39'

    // start up
    implementation "androidx.startup:startup-runtime:1.1.0"

    // retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofit_version"

    // okHttp
    def okHttp_version = "4.9.0"
    implementation "com.squareup.okhttp3:okhttp:$okHttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttp_version"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okHttp_version"

    // coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.3"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.3'

    // rxJava
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.7'

    // viewModel
    def lifecycle_version = "1.1.1"
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation "androidx.activity:activity-ktx:1.2.3"

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // viewPager2
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    // fragment
    def fragment_version = '1.2.0-rc04'
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation "androidx.fragment:fragment-testing:$fragment_version"

    // firebase
    implementation platform('com.google.firebase:firebase-bom:28.4.2')
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    // open source notices
    implementation("com.google.android.gms:play-services-oss-licenses:17.0.0")

    // soft keyboard listener
    implementation 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:3.0.0-RC3'

    // unit test
    androidTestImplementation("org.mockito:mockito-android:2.24.5")
    testImplementation 'org.mockito:mockito-inline:2.21.0'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation 'org.mockito:mockito-core:3.5.9'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation "org.robolectric:robolectric:4.4"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"

    // paging3
    def paging_version = "3.0.1"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation "androidx.paging:paging-common-ktx:$paging_version"
    implementation "androidx.paging:paging-rxjava3:$paging_version"

    // Room
    def room_version = "2.3.0"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    // Shimmer effect
    implementation 'com.facebook.shimmer:shimmer:0.5.0'

    // tag library
    implementation 'com.github.yeon-kyu:AndroidTagView:v1.1.7-alpha1'

    // WebSocket
    implementation "org.java-websocket:Java-WebSocket:1.5.1"

    // HoldableSwipeHandler
    implementation 'com.github.yeon-kyu.HoldableSwipeHandler:HoldableSwipeHandler:1.2.0'

    // leak canary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.6'

    // swipeRefreshLayout
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'

    // appsFlyer
    implementation 'com.appsflyer:af-android-sdk:6.3.2'
}

kapt {
    correctErrorTypes true
}